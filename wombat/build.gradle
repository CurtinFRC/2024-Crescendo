import edu.wpi.first.toolchain.NativePlatforms
import org.gradle.api.InvalidUserDataException

apply plugin: 'cpp'
apply plugin: 'google-test-test-suite'
apply plugin: 'edu.wpi.first.GradleRIO'

def debug = project.hasProperty("debug");
def release = project.hasProperty("release");

model {
    components {
        Wombat(NativeLibrarySpec) {
            targetPlatform NativePlatforms.desktop
            targetPlatform NativePlatforms.roborio

            sources {
                sources.cpp {
                    source {
                        srcDir 'src/main/cpp'
                        include '**/*.cpp', '**/*.cc'
                    }

                    exportedHeaders {
                        srcDir 'src/main/include'
                    }
                }
            }

            binaries.all {
                if (release && debug) {
                    throw new InvalidUserDataException("Cannot be both release and debug project");
                }

                if(!release && !debug) {
                    cppCompiler.define("RELEASE");
                }

                if(release) {
                    cppCompiler.define("RELEASE");
                }

                if (debug) {
                    cppCompiler.define("DEBUG");
                }

                if (it.targetPlatform.name == "linuxathena")
                    cppCompiler.define "PLATFORM_ROBORIO"
                else
                    cppCompiler.define "PLATFORM_DESKTOP"
            }

            wpi.cpp.vendor.cpp(it)
            wpi.cpp.deps.wpilib(it)
        }
    }
    testSuites {
        WombatTest(GoogleTestTestSuiteSpec) {
            testing $.components.Wombat;

            sources.cpp {
                source {
                    srcDir 'src/test/cpp'
                    include '**/*.cpp'
                }

                exportedHeaders {
                    srcDir 'src/test/include'
                }
            }

            wpi.cpp.vendor.cpp(it)
            wpi.cpp.deps.wpilib(it)
            wpi.cpp.deps.googleTest(it)
        }
    }
}
